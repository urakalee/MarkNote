apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "me.urakalee.next2"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            Properties properties = new Properties()
            InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream();
            properties.load(inputStream)

            storeFile file(properties.getProperty('keystore.file'))
            storePassword properties.getProperty('keystore.password')
            keyAlias properties.getProperty('keystore.name')
            keyPassword properties.getProperty('keystore.password')

            v2SigningEnabled false
        }
        release {}
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true // enable shrink resources
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                outputFileName = defaultConfig.applicationId + "-" + variant.baseName + "-" + defaultConfig.versionName + "-" + defaultConfig.versionCode + ".apk";
            }
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // ref: https://youtrack.jetbrains.com/issue/KT-4779 & https://kotlinlang.org/api/latest/jvm/stdlib/kotlin.jvm/-jvm-default/index.html
    // ref: https://stackoverflow.com/questions/44141076/compilekotlin-block-in-build-gradle-file-throws-error-could-not-find-method-com
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = '1.8'
            // enable-jvm-default is only for avoid IDE warning
            freeCompilerArgs = ["-Xenable-jvm-default", "-Xjvm-default=enable"]
        }
    }
    /*
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    */
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    flavorDimensions "default"
    productFlavors {
        dev {}
        alpha {}
        beta {}
        prod {}
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    kapt "com.android.databinding:compiler:$androidPluginVersion"
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:palette-v7:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:preference-v7:$supportLibVersion"
    implementation "com.android.support:customtabs:$supportLibVersion"
    implementation "android.arch.lifecycle:runtime:$archVersion"
    implementation "android.arch.lifecycle:extensions:$archVersion"
    kapt "android.arch.lifecycle:compiler:$archVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.github.bumptech.glide:glide:3.8.0'
    implementation 'commons-io:commons-io:2.5'
    implementation 'com.larswerkman:HoloColorPicker:1.5'
    implementation 'org.ocpsoft.prettytime:prettytime:4.0.1.Final'
    implementation 'com.github.flavienlaurent.datetimepicker:library:0.0.2'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'com.balysv.materialmenu:material-menu:2.0.0'
    implementation('com.afollestad.material-dialogs:core:0.9.4.5') {
        exclude group: 'com.android.support', module: 'support-v13'
    }
    implementation('com.afollestad.material-dialogs:commons:0.9.4.5') {
        exclude group: 'com.android.support', module: 'support-v13'
    }
    implementation 'com.koushikdutta.ion:ion:2.1.7'
    implementation 'com.github.oxoooo:pull-back-layout:1.0.1'
    implementation 'com.squareup.okhttp3:okhttp:3.8.0'
    implementation 'com.squareup.retrofit2:retrofit:2.1.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.1'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.30'
    implementation 'com.github.lecho:hellocharts-android:v1.5.8'
    implementation 'com.andrognito.pinlockview:pinlockview:2.1.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }
    implementation('com.crashlytics.sdk.android:answers:1.4.2@aar') {
        transitive = true
    }
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'joda-time:joda-time:2.9.9'
    implementation 'com.github.Kennyc1012:BottomSheet:2.4.0'
    implementation 'com.github.gabrielemariotti.changeloglib:changelog:2.0.0'
    // leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    implementation 'top.zibin:Luban:1.1.3'
    implementation project(path: ':markdown')
    // share
    implementation'com.tencent.mm.opensdk:wechat-sdk-android-with-mta:5.1.4'
}
